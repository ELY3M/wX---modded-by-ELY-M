apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    namespace 'joshuatee.wx'
    compileSdkVersion 33
    buildToolsVersion '33.0.0'


    defaultConfig {
        applicationId "joshuatee.wx"
        minSdkVersion 23
        targetSdkVersion 33
        // Stops the Gradle pluginâ€™s automatic rasterization of vectors (2016_02_25 add 23.2 )
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    sourceSets {
        main {
            jni.srcDirs = [] //disable automatic ndk-build
            jniLibs.srcDir 'src/main/libs'
        }
    }


    def ndkDir = "D:\\android-sdk\\android-ndk-r25b"

    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        commandLine "$ndkDir\\ndk-build.cmd",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all'

    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        commandLine "$ndkDir\\ndk-build.cmd",
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

    // Flag to tell aapt to keep the attribute ids around - (2016_02_25 add 23.2 )
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'Typos'
        checkReleaseBuilds false
        abortOnError false
    }

    // Enables Jetpack Compose for this module
    /*buildFeatures {
        compose true
    }*/

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    // https://developer.android.com/jetpack/androidx/versions
    implementation 'com.google.android.material:material:1.7.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.media:media:1.6.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.jjoe64:graphview:4.2.2'
    implementation "com.squareup.okhttp3:okhttp:4.10.0"
    // implementation "androidx.navigation:navigation-compose:1.0.0-alpha07"

    // https://developer.android.com/studio/write/java8-support
    // https://developer.android.com/studio/write/java8-support-table
    // https://developer.android.com/reference/java/time/package-summary
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.8'
    
    //elys mods
    implementation 'ch.acra:acra-mail:5.9.6'
    implementation 'ch.acra:acra-core:5.9.6'
    //implementation 'io.github.nishkarsh:android-permissions:0.1.7'
    implementation 'io.github.nishkarsh:android-permissions:2.1.6'
    implementation 'org.shredzone.commons:commons-suncalc:3.5'
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://maven.google.com"
    }
}



