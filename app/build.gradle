apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    namespace 'joshuatee.wx'
//    compileSdkVersion 33
//    buildToolsVersion '33.0.1'

    defaultConfig {
        compileSdk 34
    }

    defaultConfig {
        applicationId "joshuatee.wx"
        // 2023-09-09 changed from 23 to 25
        minSdkVersion 25
        targetSdkVersion 34
        // Stops the Gradle pluginâ€™s automatic rasterization of vectors (2016_02_25 add 23.2 )
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    sourceSets {
        main {
            //disable automatic ndk-build
            jniLibs.srcDir 'src/main/libs'
        }
    }


    def ndkDir = "D:\\android-sdk\\android-ndk-r25c"

    tasks.register('buildNative', Exec) {
        commandLine "$ndkDir\\ndk-build.cmd",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all'

    }

    tasks.register('cleanNative', Exec) {
        commandLine "$ndkDir\\ndk-build.cmd",
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile).configureEach {
        compileTask -> compileTask.dependsOn buildNative
    }

    // Flag to tell aapt to keep the attribute ids around - (2016_02_25 add 23.2 )
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'Typos'
        checkReleaseBuilds false
        abortOnError false
    }

    // Enables Jetpack Compose for this module
    /*buildFeatures {
        compose true
    }*/

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    // https://developer.android.com/jetpack/androidx/versions
    implementation 'com.google.android.material:material:1.10.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.media:media:1.6.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.jjoe64:graphview:4.2.2'
    implementation "com.squareup.okhttp3:okhttp:4.11.0"
    // implementation "androidx.navigation:navigation-compose:1.0.0-alpha07"

    // https://developer.android.com/studio/write/java8-support
    // https://developer.android.com/studio/write/java8-support-table
    // https://developer.android.com/reference/java/time/package-summary
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    
    //elys mods
    implementation 'ch.acra:acra-mail:5.9.7'
    implementation 'ch.acra:acra-core:5.9.7'
    implementation 'com.markodevcic:peko:3.0.1'
    implementation 'org.shredzone.commons:commons-suncalc:3.5'
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://maven.google.com"
    }
}



