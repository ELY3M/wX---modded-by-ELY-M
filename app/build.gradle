apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'


    defaultConfig {
        applicationId "joshuatee.wx"
        minSdkVersion 16   // 4.1
        targetSdkVersion 28 // 8.1
        // Stops the Gradle pluginâ€™s automatic rasterization of vectors (2016_02_25 add 23.2 )
        //generatedDensities = []
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    sourceSets {
        main {
            jni.srcDirs = [] //disable automatic ndk-build
            jniLibs.srcDir 'src/main/libs'
        }
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndkDir = properties.getProperty('ndk.dir')

    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        commandLine "$ndkDir\\ndk-build.cmd",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all'

    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        commandLine "$ndkDir\\ndk-build.cmd",
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    clean.dependsOn 'cleanNative'


    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }


    // Flag to tell aapt to keep the attribute ids around - (2016_02_25 add 23.2 )
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'Typos'
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.media:media:1.0.1"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation 'com.jjoe64:graphview:4.2.2'
    implementation 'com.intentfilter:android-permissions:0.1.7'
    implementation 'com.beust:klaxon:5.0.1'
    implementation 'com.jayway.jsonpath:json-path:2.4.0'
    implementation 'org.shredzone.commons:commons-suncalc:2.4'
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"
}

repositories {
    mavenCentral()
    google()
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}



